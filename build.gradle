/*
 * Copyright ish group pty ltd. All rights reserved. https://www.ish.com.au
 * No copying or use of this code is allowed without permission in writing from ish.
 */

buildscript {
    repositories {
        maven {
            url 'https://repo.ish.com.au/content/groups/public'
        }
    }

    dependencies {
        classpath "org.apache.cayenne.plugins:cayenne-gradle-plugin:4.1"
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.33.0' // adds the task 'dependencyUpdates -Drevision=release'
    id 'com.dorongold.task-tree' version '1.5' // adds the task 'taskTree [some task]'
}
apply plugin: 'idea'

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    compileGroovy.groovyOptions.configurationScript = file("${rootDir}/groovyConfig.groovy")

    sourceCompatibility = 11
    repositories {
        maven {
            url 'https://repo.ish.com.au/repository/ish-snapshots'
        }
        maven {
            url 'https://repo.ish.com.au/repository/public'
        }
        maven {
            url 'https://repo.ish.com.au/repository/ish'
        }
    }

    sourceSets.main.java.srcDirs = []
    sourceSets.main.groovy.srcDir 'src/main/java'

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    dependencies {
        implementation group: "ish.oncourse.angel", name: "server-jar", version: "99-SNAPSHOT", changing: true
    }

    group = 'ish.oncourse.commercial.plugin'
    version = project.hasProperty('releaseVersion') && project.releaseVersion ? project.releaseVersion : "99-SNAPSHOT"

    publishing {
        repositories {
            maven {
                name = "ish"
                def repo = version.endsWith('SNAPSHOT') ? 'ish-snapshots' : 'ish-releases'
                url = "https://repo.ish.com.au/content/repositories/${repo}"
                credentials {
                    username nexusUsername
                    password nexusPassword
                }
            }
        }
    }
}
