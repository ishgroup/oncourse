buildscript {
	repositories {
		maven {
			url 'http://repo.ish.com.au/content/groups/public'
		}
		maven {
			url 'http://repo.ish.com.au/content/groups/ish'
		}
	}

	dependencies {
		classpath 'edu.sc.seis:gradle-macappbundle:1.0.8-b3'
		classpath 'gradle_cobertura:gradle_cobertura:1.2'
		classpath 'eu.appsatori:gradle-fatjar-plugin:0.1.2'
		classpath 'com.github.ben-manes:gradle-versions-plugin:0.3'
        classpath 'au.com.ish.gradle:release:2.1.2'
		classpath 'org.apache.cayenne:cayenne-tools:3.1B2'
	}
}

apply plugin: 'release'
release {
	//use subversion scm, unless otherwise specified in with -PscmFlavour= or in users gradle properties
	scm = project.hasProperty('scmFlavour') ? project.scmFlavour:'svn'
	failOnSnapshotDependencies = true

	//properties for svn access
	username = project.hasProperty('username') ? project.username : ''
	password = project.hasProperty('password') ? project.password : ''
	
	//the following two params have to be converted from string to boolean
	releaseDryRun = project.hasProperty('releaseDryRun') ? project.releaseDryRun=='true' : ''
	allowLocalModifications = project.hasProperty('allowLocalModifications') ? project.allowLocalModifications='true' : ''

}


allprojects {
	version = release.projectVersion
	
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'versions' // adds the task 'dependencyUpdates'

	sourceCompatibility = 1.6
	targetCompatibility = 1.6

	configurations {
		deployerJars {
			description = 'Jars needed for doing deployment to JBoss Nexus repo'
		}
	}

	repositories {
		maven {
			url 'http://repo.ish.com.au/content/groups/public'
		}
		maven {
			url 'http://repo.ish.com.au/content/groups/ish'
		}
	}

	dependencies {
		testCompile 'junit:junit:4.11'
		compile 'log4j:log4j:1.2.17'
		deployerJars 'org.apache.maven.wagon:wagon-http:2.3'
	}

	uploadArchives.dependsOn install
	group = 'ish.oncourse.angel'
	uploadArchives {
		repositories.mavenDeployer {
			name = 'ishRepo'
			configuration = configurations.deployerJars
			repository(id:'internal-releases', url: 'http://repo.ish.com.au/content/repositories/ish-releases') {
				authentication(userName: nexusUsername, password: nexusPassword)
			}
			snapshotRepository(id:'internal-snapshots', url: 'http://repo.ish.com.au/content/repositories/ish-snapshots') {
				authentication(userName: nexusUsername, password: nexusPassword)
			}
		}
	}

	ext.sharedManifest = manifest {
		attributes 'SCM-Revision': project.release.scmVersion,
			'Release-Version': version,
			'Built-Date': System.getProperty('build_number') ? new Date(): new Date().format("yy-MMM-dd"),
			'Built-JDK': System.getProperty('java.version'),
			'Built-Gradle': gradle.gradleVersion,
			'Implementation-Version': System.getProperty('build_number') ?: 'local'
	}

	test {
		systemProperties 'java.awt.headless': 'true'
		maxHeapSize = '1024m'
		jvmArgs "-XX:MaxPermSize=256m"
		ignoreFailures = true
		
		// log each starting test
  		beforeTest { descriptor -> logger.lifecycle("Running: " + descriptor)
  }
	}
	
	if (project.gradle.getStartParameter().getTaskNames().contains("tasks")) {
    	task cobertura << {
        	throw new IllegalStateException("Because of a bug in Gradle, you can't run cobertura and tasks together")
    	}
    
    	cobertura.description = """Generate Cobertura code coverage report
		======================== NOTE ==========================
		There is a workaround that has to do with gradle issue 2023 in the build file.
		https://github.com/valkolovos/gradle_cobertura/issues/18
		http://issues.gradle.org//browse/GRADLE-2023
		"""
	} else {
	    apply plugin: 'cobertura'
	    cobertura {
			coverageFormat = 'xml'
		}
	}

}
	
configure(subprojects.findAll {it.name == 'server' || it.name == 'client'}) { 

	project.ext {
		appName = getProject().name == 'server' ? "onCourseServer" : "onCourse"
	}

	apply plugin: 'fatjar'

	fatJarPrepareFiles {
		doLast {
			//copy few extra files, the fatjar plugin does not work with project fileset
			// the plugin's configurable 'include' is just a filter on the default fileset
			logger.lifecycle("copying generated sources")
			copy {
				into "$buildDir/tmp/fatjar-stage"
				from "$buildDir/generated-resources/main"
			}
		}
	}

	fatJar {
		// use the same manifest as normal jar
		manifest {
			from project.jar.manifest
		}

		// add a classifier to distinguish farjar from normal jar
		classifier = 'all'

		// do not include the certificate files
		exclude 'META-INF/*.SF'
		exclude 'META-INF/*.DSA'
		exclude 'META-INF/*.RSA'
	}

	task prepareKeychains {
		doLast{
			if (project.hasProperty('osxKeychainPassword')) {
				logger.lifecycle("Unlocking the keychain: "+project.osxKeychain)
				exec {
					executable = 'security'
					args = ['unlock-keychain', '-p', project.osxKeychainPassword, project.osxKeychain]
				}
				logger.lifecycle("Unlocking the keychain: "+project.windowsKeychain)
				exec {
					executable = 'security'
					args = ['unlock-keychain', '-p', project.osxKeychainPassword, project.windowsKeychain]
				}
				logger.lifecycle("Exporting the windows keychain into pkcs12 format: "+project.codeSigningTemporaryKeystore)
				exec {
					executable= 'security'
					args = ['export', 	'-k', project.windowsKeychain,  
							'-t', 'all', 
							'-f', 'pkcs12',
							'-P', project.codeSigningTemporaryPassword, 
							'-o', project.codeSigningTemporaryKeystore]
				}
			} else {
				logger.lifecycle("Cannot unlock keychain, please provide osxKeychainPassword in gradle.properties")
			}
		}
	}
	

	task bundle(dependsOn: tasks.findAll { task -> task.name.startsWith('bundle_') }) {
		description "Create all application executables."
	}

	bundle.dependsOn {
		project.tasks.findAll { it.name.startsWith('bundle_') }
	}

	task installer(){
		description "Create all application installers."
	}
	installer.dependsOn {
		project.tasks.findAll { it.name.startsWith('installer_') }
	}
}

task testReport(type: TestReport) {
	destinationDir = file("$buildDir/reports/tests")
	// Include the results from the `test` task in all subprojects
	reportOn subprojects*.test
}

apply plugin: "sonar-runner"
sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://sonar.ish.com.au"
        property "sonar.jdbc.url", "jdbc:mysql://delish.ish.com.au:3306/sonar?useUnicode=true&amp;characterEncoding=utf8"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.username", 'sonar'
        property "sonar.password", sonarPassword // sonar password comes from users gradle.properties file
    }
}

subprojects { 
	version = project.parent.version
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.5"
}