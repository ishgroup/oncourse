buildscript {
	repositories {
		//mavenCentral()

		maven {
			url 'http://repo.ish.com.au/content/groups/public'
		}
		maven {
			url 'http://repo.ish.com.au/content/groups/ish'
		}
	}
	dependencies {
		classpath 'org.apache.cayenne:cayenne-tools:3.1'
		classpath 'au.com.ish.gradle:release:2.2.2'
		classpath 'com.github.ben-manes:gradle-versions-plugin:0.7'
		classpath 'org.gradle.plugins:gradle-compass:1.0.10'
		classpath 'com.eriwen:gradle-js-plugin:1.12.1'
	}
}

allprojects {
	apply plugin: 'com.github.ben-manes.versions' // adds the task 'dependencyUpdates -Drevision=release'
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'release'
	apply plugin: 'jacoco'
    apply plugin: 'compass'
    apply plugin: 'com.eriwen.gradle.js'

    //java compile should read java files in UTF-8
    //because some tests (ish.oncourse.services.textile.TextileUtilTest.test_unicodeQuotesEncoding)
    //use UTF-8 chars
    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    release {
		//use subversion scm, unless otherwise specified in with -PscmFlavour= or in users gradle properties
		scm = project.hasProperty('scmFlavour') ? project.scmFlavour:'svn'
		failOnSnapshotDependencies = true

		//properties for svn access
		username = project.hasProperty('username') ? project.username : ''
		password = project.hasProperty('password') ? project.password : ''

		//the following two params have to be converted from string to boolean
		releaseDryRun = project.hasProperty('releaseDryRun') ? project.releaseDryRun=='true' : ''
		allowLocalModifications = project.hasProperty('allowLocalModifications') ? project.allowLocalModifications='true' : ''
	}
	version = release.projectVersion

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	configurations {
		deployerJars {
			description = 'Jars needed for doing deployment to JBoss Nexus repo'
		}
	}

	repositories {
		//mavenCentral()
		maven {
			url 'http://repo.ish.com.au/content/groups/public'
		}
		maven {
			url 'http://repo.ish.com.au/content/groups/ish'
		}
	}

	dependencies {
		testCompile 'junit:junit:4.12'
		testCompile 'dbunit:dbunit:2.4.8'
		testCompile 'org.mockito:mockito-core:1.10.19'

		compile 'log4j:log4j:1.2.17'

		deployerJars 'org.apache.maven.wagon:wagon-http:2.8'
	}

	uploadArchives.dependsOn install
	group = 'ish.oncourse.willow'
	uploadArchives {
		repositories.mavenDeployer {
			name = 'ishRepo'
			configuration = configurations.deployerJars
			repository(id:'internal-releases', url: 'http://repo.ish.com.au/content/repositories/ish-releases') {
				authentication(userName: nexusUsername, password: nexusPassword)
			}
			snapshotRepository(id:'internal-snapshots', url: 'http://repo.ish.com.au/content/repositories/ish-snapshots') {
				authentication(userName: nexusUsername, password: nexusPassword)
			}
		}
	}
	uploadArchives.dependsOn install

	ext.sharedManifest = manifest {
		attributes 'Release-Version': version,
			'Built-Date': new Date(),
			'Built-JDK': System.getProperty('java.version'),
			'Built-Gradle': gradle.gradleVersion,
			'Implementation-Version': System.getProperty('build_number') ?: 'local'
//			'SCM-Revision': project.release.scmVersion
	}

	jar {
		manifest {
			from sharedManifest
		}
	}

	test {
		systemProperties 'java.awt.headless': 'true'
		maxHeapSize = '1024m'
		jvmArgs "-XX:MaxPermSize=256m"
		ignoreFailures = true

		jacoco {
			enabled = project.hasProperty('jacoco-enabled') ? true : false;
		}

		// log each starting test
		beforeTest { descriptor -> logger.lifecycle("Running: " + descriptor)
		}
	}

    //common tasks for combining java script and css
    javascript.source {
        production {
            js {
                srcDir 'src/main/static/js'
                include "**/*.js"
            }
        }
    }

    minifyJs {
        source = javascript.source.production.js.files
        dest = file("${buildDir}/resource-assemble/js/${project.name}-min.js")
    }

    gzipJs {
        source = minifyJs
        dest = file("${buildDir}/resource-assemble/js/${project.name}-min.js.gz")
    }

//css combining
    compass {
        cssDir = file("${buildDir}/resource-assemble/css")
        sassDir = file('src/main/static/src')
        fontsDir = file('src/main/static/fonts')
        noLineComments = true
        relativeAssets = true
        debugInfo = false
        environment = 'production'
    }

    task gzipCss(dependsOn:compileSass) {
        String srcPath = "${buildDir}/resource-assemble/css/${project.name}.css"
        inputs.file srcPath
        outputs.file "${srcPath}.gz"
        doLast {
            ant.gzip(src: srcPath, destfile: "${srcPath}.gz")
        }
    }
}

apply plugin: "sonar-runner"
sonarRunner {
	sonarProperties {
		property "sonar.host.url", "http://sonar.ish.com.au"
		property "sonar.jdbc.url", "jdbc:mysql://delish.ish.com.au:3306/sonar"
		property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
		property "sonar.jdbc.username", 'sonar'
		property "sonar.jdbc.password", sonarPassword // sonar password comes from users gradle.properties file
		property "sonar.links.scm", "https://svn.ish.com.au/ish/willow/trunk"
		property "sonar.links.ci", "http://build.ish.com.au/job/willow-sonar"
		property "sonar.links.issue", "https://squish.ish.com.au"
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = "2.3"
}