apply from: "$rootDir/gradle/tapestry.app.gradle"

configurations {
	all*.exclude group: 'org.apache.tapestry', module: 'tapestry-spring'
}

ext {
	tapestryVersion = '5.7.1'
	adminCxfVersion = '2.6.6'
}

dependencies {
	implementation project(':common:common-bootique')

	implementation "org.codehaus.groovy:groovy-all:3.0.7"
	implementation "org.codehaus.groovy:groovy-dateutil:3.0.7"

	implementation 'javax.xml.ws:jaxws-api:2.3.1'
	implementation 'javax.jws:javax.jws-api:1.1'

	/**
	 * tapestry dependencies
	 */
	implementation "org.apache.tapestry:tapestry-core:$tapestryVersion"
	implementation "org.apache.tapestry:plastic:$tapestryVersion"
	implementation "org.apache.tapestry:tapestry5-annotations:$tapestryVersion"
	implementation "org.apache.tapestry:tapestry-ioc:$tapestryVersion"
	implementation "org.apache.tapestry:tapestry-json:$tapestryVersion"
	implementation "org.apache.tapestry:tapestry-spring:$tapestryVersion"
	implementation "org.apache.tapestry:tapestry-upload:$tapestryVersion"

	testCompile ("org.apache.tapestry:tapestry-test:$tapestryVersion") {
		exclude group: 'org.seleniumhq.selenium.server', module: 'selenium-server'
	}

	implementation project(':common:cayenne-model')
	implementation project(':common:common-services')
	implementation (project(':common:webservices-usi')) {
		exclude group: 'axis'
		exclude group: 'org.apache.cxf'
		exclude group: 'org.springframework'
		exclude group: 'ish.oncourse.willow'
	}

	implementation ("org.apache.cxf:cxf-rt-frontend-jaxws:$adminCxfVersion") {
		exclude group: 'org.apache.geronimo.specs', module: 'geronimo-servlet_3.0_spec'
		exclude group: 'org.apache.geronimo.specs', module: 'geronimo-javamail_1.4_spec'
	}

	implementation "org.apache.cxf:cxf-rt-ws-security:$adminCxfVersion"
	implementation "org.apache.cxf:cxf-rt-transports-http:$adminCxfVersion"
	implementation 'xalan:xalan:2.7.2'
	implementation 'wsdl4j:wsdl4j:1.6.3'

	implementation 'org.reflections:reflections:0.9.11'
	implementation 'commons-dbcp:commons-dbcp:1.4'
	implementation 'commons-pool:commons-pool:1.5.6'

	// The odd next line is to work around gradle pulling in the wrong configuration
	// http://forums.gradle.org/gradle/topics/transitive_libraries_not_included_through_project_dependency
	testImplementation project(path: ':common:common-test', configuration: 'testFixtures')

	testImplementation project(':common:test-functions')
	testImplementation "org.dbunit:dbunit:2.5.3"

}


mainClassName = 'ish.oncourse.admin.AdminApp'

targetCompatibility = '11'
sourceCompatibility = '11'

ext {
	staticSrcPath = "${projectDir}/src/main/static"
	staticBuildPath = "${buildDir}/resource-assemble/static"
	cssCombined = "${staticBuildPath}/css/${project.name}.css"
}

//css combining
task sass(type: Exec) {
	inputs.dir "${staticSrcPath}/src"
	outputs.file cssCombined
	commandLine "sassc", "--style", "compressed", "${staticSrcPath}/src/${project.name}.scss", cssCombined
}

task gzipCss(dependsOn: sass) {
	inputs.file cssCombined
	outputs.file "${cssCombined}.gz"
	doLast {
		ant.gzip(src: cssCombined, destfile: "${cssCombined}.gz")
	}
}


/**
 * Task build and copy js and css bundles to this artifact folder
 */
task refreshIdeaArtifact {
	doFirst {
		ex.execute()
		cp.execute()
	}
	task ex(type: GradleBuild){
		tasks = ['gzipJs', 'gzipCss']
	}
	task cp(type: Copy) {
		from(staticBuildPath) {
			include '**/*.*'
		}
		from(staticSrcPath) {
			include '**/*.*'
		}
		into "out/resources/static"
	}
}

processResources.inputs.files gzipCss

jar {
	from "${buildDir}/resource-assemble"
	from ('src/main') {
		include 'static/**/*'
		exclude 'static/src'
	}
}
