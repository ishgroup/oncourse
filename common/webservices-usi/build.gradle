dependencies {
    compile project(':common:common-services')
    compile ('ish.oncourse.willow:waCommon:5.1.42') {
        exclude group: 'org.apache.cayenne', module: 'cayenne-server'
    }

    compile 'org.apache.cxf:cxf-rt-frontend-jaxws:2.6.16'
    compile 'org.apache.cxf:cxf-rt-ws-addr:2.6.16'
    compile 'org.apache.cxf:cxf-rt-ws-policy:2.6.16'
    compile 'org.apache.cxf:cxf-rt-ws-security:2.6.16'
    compile 'org.apache.cxf:cxf-rt-ws-mex:2.6.16'
    compile 'org.apache.cxf:cxf-rt-transports-http-jetty:2.6.16'
    
    compile 'org.springframework:spring-beans:3.0.5.RELEASE'
    compile 'org.bouncycastle:bcprov-jdk15:1.45'
}

jar {
    manifest {
        from sharedManifest
    }
}


configurations {
    ws
}

dependencies{
    ws "org.apache.cxf:cxf-tools:2.6.16",
            "org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:2.6.16",
            "org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:2.6.16",
            "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.5"
    "log4j:log4j:1.2.17"
}

project.ext {
    wsdlDir = file("${projectDir}/src/main/resources/wsdl")
    generatedWsdlDir = file("${projectDir}/src/main/java")
    wsdlsToGenerate = [
            [       '-wsdlLocation', "/wsdl/USIService.wsdl",
                    '-b', "$wsdlDir/usi_bindings.xml",
                    "-client",
                    "$wsdlDir/USIService.wsdl"],
    ]
}

task wsdl2Java() {
    inputs.files wsdlDir.listFiles()
    outputs.files generatedWsdlDir

    doLast {
        wsdlsToGenerate.each { argsin ->
            argsin.add(argsin.size - 1, '-d')
            argsin.add(argsin.size - 1, generatedWsdlDir)
            javaexec {
                classpath configurations.ws
                main = 'org.apache.cxf.tools.wsdlto.WSDLToJava'
                args = argsin
                systemProperties = ['exitOnFinish':'TRUE']
            }
        }
    }
}

