ext {
    cxfVersion = '3.2.2'
}
dependencies {
    compile project(':common:waCommon')
    compile ("org.apache.cxf:cxf-core:$cxfVersion")
    compile ("org.apache.cxf:cxf-rt-frontend-jaxws:$cxfVersion") {
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-javamail_1.4_spec'
    }
    compile ("org.apache.cxf:cxf-rt-transports-http:$cxfVersion")
    compile ("org.apache.cxf:cxf-rt-ws-security:$cxfVersion") {
        exclude group:'net.sf.ehcache', module: 'ehcache-core'
    }
    compile ("org.apache.cxf:cxf-rt-ws-security:cxf-rt-databinding-jaxb:$cxfVersion")
}

jar {
	manifest {
		from sharedManifest
	}
}

configurations {
    all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-web'
    ws
}
 
dependencies{
    ws "org.apache.cxf:cxf-tools:2.6.16",
        "org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:$cxfVersion",
        "org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:$cxfVersion",
        "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.5"
        "log4j:log4j:1.2.17"
}

task wsdl2Java() {
    def wsdlDir = file("${projectDir}/src/main/resources/wsdl")
    def generatedWsdlDir = file("${projectDir}/src/main/java")
            
    inputs.files wsdlDir.listFiles()
    outputs.files generatedWsdlDir
    
    doLast {
        replicationVersions.each { v ->
            javaexec {
                classpath configurations.ws
                main = 'org.apache.cxf.tools.wsdlto.WSDLToJava'
                args = ['-xjc', '-xjc-Xinheritance',
                        '-wsdlLocation', "/wsdl/v${v}_replication.wsdl",
                        '-p', "ish.oncourse.webservices.v${v}.stubs.replication",
                        '-b', "${wsdlDir}/replication${v}_binding.xml",
                        '-d', generatedWsdlDir,
                        "${wsdlDir}/v${v}_replication.wsdl"]
                systemProperties = ['exitOnFinish':'TRUE']
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/generated'
        }
    }
}

task generateSources(type: Copy) {
    from 'src/templates'
    into 'src/main/generated'
    expand(replicationVersions: replicationVersions, referenceVersions: referenceVersions)
}