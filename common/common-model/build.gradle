configurations {
	cayenneTools
}


dependencies {
	cayenneTools "org.apache.cayenne:cayenne-tools:$cayenneVersion"
}

dependencies {
    compile ('ish.oncourse.willow:waCommon:5.1.71') {
        //we should exclude cayenne-server from waCommon because the cayenne-server is not compatable to willow code.
        exclude group: 'org.apache.cayenne', module: 'cayenne-server'
    }

    compile "org.apache.cayenne:cayenne-server:$cayenneVersion"
	compile "org.apache.cayenne:cayenne-lifecycle:$cayenneVersion"
	compile 'joda-time:joda-time:1.6.2'
	compile 'commons-lang:commons-lang:2.6'
	compile 'opensymphony:oscache:2.4.1'
	compile 'concurrent:concurrent:1.3.4'
	compile 'org.apache.tapestry:tapestry-ioc:5.2.6'
    compile ('commons-validator:commons-validator:1.3.1'){
        //the lib (version 2.1) conflicts with commons-collections (ver 3.2.1) from org.apache.cayenne group dependences
        exclude group:'commons-collections', module: 'commons-collections'
    }
	compile 'oro:oro:2.0.8'
	compile 'org.liquibase:liquibase-core:2.0.1'
	/**
	 * 	we use the lib to validate email address
	 * 	also we use the lib for our MailService from common-services projects
	 */
	compile 'javax.mail:mail:1.4.5'
	compile 'javax.activation:activation:1.1.1'

    // The odd next line is to work around gradle pulling in the wrong configuration
	// http://forums.gradle.org/gradle/topics/transitive_libraries_not_included_through_project_dependency
	testCompile project(path: ':common:common-test', configuration: 'testFixtures')
}

task cayenne << {
	ant.taskdef(resource: 'org/apache/cayenne/tools/antlib.xml', classpath: project.configurations.cayenneTools.asPath)

	ant.cgen(map: file("${projectDir}/src/main/resources/oncourse.map.xml"),
			superPkg: "ish.oncourse.model.auto",
			destDir: file("${projectDir}/src/main/java"),
			makepairs: true,
			mode: 'all',
			overwrite: true
	)
}

compileJava.dependsOn(cayenne)
