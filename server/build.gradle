apply plugin: 'cayenne'

dependencies {
	def jettyVersion = '8.1.16.v20140903'

	compile project(':common')
	compile ('ish.oncourse.willow:webservices-client:5.1.56') {

        exclude group: 'javax.xml.soap'
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-ws-metadata_2.0_spec'

        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-stax-api_1.0_spec'
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-jaxws_2.2_spec'
        exclude group: 'antlr'
        exclude group: 'org.apache.xmlbeans'

        exclude group: 'javax.xml.bind'

        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-activation_1.1_spec'
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-annotation_1.0_spec'
        exclude group: 'org.eclipse.jetty'

        exclude group: 'org.slf4j'
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-servlet_2.5_spec'
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-jms_1.1_spec'
        exclude group: 'aopalliance'
        exclude group: 'org.springframework'

        exclude group: 'com.sun.xml.messaging.saaj'
        exclude group: 'org.jvnet.mimepull'
        exclude group: 'rhino'
        exclude group: 'javax.ws.rs'
        exclude group: 'net.oauth.core'
        exclude group: 'net.sf.ehcache'
        exclude group: 'org.apache.ws.security'

        exclude group: 'org.opensaml'
        exclude group: 'org.apache.santuario'

        exclude group: 'joda-time'
        exclude group: 'commons-logging'
        exclude group: 'xalan'
        
    }
    
	compile 'org.apache.cayenne:cayenne-lifecycle:3.2M1.2'

	compile 'foxtrot:foxtrot-core:4.0'

	compile "org.eclipse.jetty:jetty-servlet:$jettyVersion"
	compile "org.eclipse.jetty:jetty-servlets:$jettyVersion"

	compile 'org.apache.derby:derbyclient:10.11.1.1'
	compile 'org.apache.derby:derbynet:10.11.1.1'
	compile 'org.liquibase:liquibase-core:3.3.1'

	compile 'net.sourceforge.jtds:jtds:1.3.1'
	compile 'mysql:mysql-connector-java:5.1.34'
	compile 'net.sf.saxon:Saxon-HE:9.6.0-3'
	compile 'org.quartz-scheduler:quartz:2.2.1'
    //log4j2 to slf4j1.7 bridge
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.1'

	compile 'commons-lang:commons-lang:2.6' // this exists only to support some old reports. To be removed when all reports upgraded.
    compile ('net.sf.jasperreports:jasperreports:5.1.2') {
        exclude group: 'org.apache.poi', module: 'poi-ooxml'

        //excluding the following, because they aren't correctly defined inside dependencies, causing duplicate jars to be added to the project
        exclude group: 'bouncycastle', module: 'bcmail-jdk14'
        exclude group: 'bouncycastle', module: 'bcprov-jdk14'
        exclude group: 'bouncycastle', module: 'bctsp-jdk14'
    }

    compile ('org.bouncycastle:bctsp-jdk14:1.46') {
    	exclude group: 'org.bouncycastle', module: 'bcmail-jdk14'
    }

    compile 'org.codehaus.groovy:groovy-templates:2.3.9'
    compile 'org.codehaus.groovy:groovy-jsr223:2.3.9'

	compile 'com.warrenstrange:googleauth:0.4.3'
	compile 'net.glxn.qrgen:javase:2.0'

	testCompile 'org.mockito:mockito-core:1.10.17'
	testCompile 'org.apache.derby:derbytools:10.11.1.1'
	testCompile "org.eclipse.jetty:test-jetty-servlet:$jettyVersion"
	testCompile 'dbunit:dbunit:2.4.8'
	testCompile 'org.reflections:reflections:0.9.9'
	testCompile 'commons-dbcp:commons-dbcp:1.4'
	testCompile 'xmlunit:xmlunit:1.5' //used for xml export validation tests
}

jar {
	manifest {
		from sharedManifest
		attributes 'Main-Class': 'ish.oncourse.server.AngelServer'
	}
}

sourceSets {
  main {
	// generate some manifest files for lists of resources to find at runtime
	output.dir("$buildDir/generated-resources/main", builtBy: 'createResourceManifests')
  }
}

configurations {
	apiDocs
}

dependencies {
	apiDocs 'ish.oncourse.willow:waCommon:5.1.57:sources'
}

task scriptApiDocs(type: Javadoc) {
	source = sourceSets.main.allJava.plus(zipTree(configurations.apiDocs.find { 
		it.name.startsWith("waCommon") 
	})).matching {
		include '**/*.java'
	}
	destinationDir = reporting.file("script-api-docs")
	failOnError = false
	classpath = configurations.compile

	options {
		// this is ugly but it seems like there is no other way to get to the buildSrc jars
		docletpath = rootProject.files('buildSrc/script-api-doclet/build/libs/script-api-doclet.jar').asList()
		doclet = "ish.tools.ScriptApiDoclet"
		stylesheetFile = rootProject.file('buildSrc/script-api-doclet/src/main/resources/javadocs.css')
		windowTitle = "onCourse scripting API"
	}
}

task createResourceManifests {
	doLast {
		def sources = ["resources/schema/referenceData/exportTemplates", "resources/schema/referenceData/reports"]
		sources.each { s ->
			def exportManifest = new File("$buildDir/generated-resources/main/$s", "manifest")
			exportManifest.getParentFile().mkdirs()
			exportManifest.write("")

			FileTree tree = fileTree(dir: "src/main/resources/$s")
			tree.exclude(".DS_Store")
			tree.visit {element ->
				if (!element.isDirectory()) exportManifest.append("$s/$element.relativePath\n")
			}
		}
	}
}
test.dependsOn(createResourceManifests)

task run(dependsOn: [classes, processResources], type: JavaExec) {
	main = 'ish.oncourse.server.AngelServer'
	classpath = sourceSets.main.runtimeClasspath
	args 'DEBUG=true'
	jvmArgs '-Xmx1024m', '-XX:MaxPermSize=128M', '-XX:+HeapDumpOnOutOfMemoryError'
}

test {
	ext.dbTests = project.hasProperty('database') ? project.database : "derby-test"

	logger.lifecycle("will run tests in '"+dbTests+"' mode")

	switch (dbTests) {
		case "derby-test":
			systemProperty 'testDatabaseUri', testDatabaseUriDerby
			exclude '**/db/Mysql*.class'
			exclude '**/db/Mssql*.class'
			break

		case "mssql-test":
			assert project.hasProperty('testDatabaseUriMssql') // Please provide the testDatabaseUriMssql in your ~/.gradle/gradle.properties file
			systemProperty 'testDatabaseUri', testDatabaseUriMssql
			exclude '**/db/Mysql*.class'
			exclude '**/db/Derby*.class'
			break

		case "mysql-test":
			assert project.hasProperty('testDatabaseUriMysql') // Please provide the testDatabaseUriMysql in your ~/.gradle/gradle.properties file
			systemProperty 'testDatabaseUri', testDatabaseUriMysql
			exclude '**/db/Mssql*.class'
			exclude '**/db/Derby*.class'
			break
	}
}

cayenne {
	cayenneMap = file ("${projectDir}/../common/src/main/resources/cayenne/AngelMap.map.xml")
	superPkg = "ish.oncourse.cayenne.glue"
	serverSuperPkg = "ish.oncourse.server.cayenne.glue"
	templateDir = file ("${projectDir}/../common/src/main/resources/cayenne/dotemplates/")
	type = 'server'
	destDir = file ("${projectDir}/src/main/java")
}

appBundler {
    distributionPath = project.file("${distsDir}/macosx/")
    appName = "onCourseServer"
    appIdentifier = "ish.oncourse"
    mainClassName = "ish.oncourse.server.AngelServer"
    iconFile = new File(projectDir, "src/packaging/macosx/onCourseServer.icns")
    jarFile = new File(libsDir, "${project.name}-${project.version}-all.jar")
    keychain = project.osxKeychain
    certIdentity = project.osxKeychainIdentity

    documentTypes = [[
             extensions: "iocdata",
             icon      : "onCourseDocument.icns",
             name      : "onCourse server data file",
             role      : "Editor",
             isPackage : "true"
    ]]

    jvmOptions = [
            "-Dapple.awt.textantialiasing=true",
            "-DJFileChooser.appBundleIsTraversable=true",
            "-DJFileChooser.packageIsTraversable=true",
            "-Dapple.awt.antialiasing=true",
            "-Dapple.awt.showGrowBox=true",
            "-Xmx${project.maxmemoryServer}",
            "-XX:MaxPermSize=${project.maxpermgenServer}"
    ]
}

task installer_osx(dependsOn: [fatJar, prepareKeychains, codeSign], type: au.com.ish.DmgTask) {
    description "Create the OSX application executable and installer (which is just a dmg file)."
}

project.ext.unixAppFiles = copySpec {
	into('.') {
		from("${projectDir}/src/packaging/unix/")
		include ("*")

		from("${libsDir}/${project.name}-${version}-all.jar")
	}
}

task bundle_win(dependsOn: fatJar, type: au.com.ish.Exe4JTask) {
	configFile = file("${projectDir}/src/packaging/windows/server.exe4j.xml")
	jarFile = file("${libsDir}/${project.name}-${project.version}-all.jar")
	resourceFiles = files("${projectDir}/src/packaging/windows/onCourseServer.ico")
	exeFile = file("${distsDir}/windows/onCourseServer.exe")

	if (project.hasProperty('osxKeychainPassword')) {
		certificateFile = file (project.codeSigningTemporaryKeystore)
		certificateFilePassword= project.codeSigningTemporaryPassword
	}
}

task bundle_winService(dependsOn: fatJar, type: au.com.ish.Exe4JTask) {
	configFile = file("${projectDir}/src/packaging/windows/service.exe4j.xml")
	jarFile = file("${libsDir}/${project.name}-${project.version}-all.jar")
	resourceFiles = files("${projectDir}/src/packaging/windows/onCourseServer.ico")
	exeFile = file("${distsDir}/windows/onCourseService.exe")

	if (project.hasProperty('osxKeychainPassword')) {
		certificateFile = file (project.codeSigningTemporaryKeystore)
		certificateFilePassword= project.codeSigningTemporaryPassword
	}
}

task bundle_winService64(dependsOn: fatJar, type: au.com.ish.Exe4JTask) {
	configFile = file("${projectDir}/src/packaging/windows/service64.exe4j.xml")
	jarFile = file("${libsDir}/${project.name}-${project.version}-all.jar")
	resourceFiles = files("${projectDir}/src/packaging/windows/onCourseServer.ico")
	exeFile = file("${distsDir}/windows/onCourseService64.exe")

	if (project.hasProperty('osxKeychainPassword')) {
		certificateFile = file (project.codeSigningTemporaryKeystore)
		certificateFilePassword= project.codeSigningTemporaryPassword
	}
}

task installer_unix(dependsOn: fatJar, type: Copy) {
	description "Create the Unix/Linux package (zip)."
	doFirst {
		logger.lifecycle("Creating the unix application executable for $project.name")
	}
	with unixAppFiles
	into file("${distsDir}/unix")
	doLast {
		ant.replace(file: "${distsDir}/unix/onCourseServer.sh", token: "@version@", 			value: "${project.version}")
		ant.replace(file: "${distsDir}/unix/onCourseServer.sh", token: "@project.name@", 		value: "${project.name}")
		ant.replace(file: "${distsDir}/unix/onCourseServer.sh", token: "@maxmemoryServer64@", 	value: "${project.maxmemoryServer64}")

		ant.tar(basedir: "${distsDir}/unix/", destfile: "${distsDir}/unix/onCourseServer.tar")
		ant.gzip(zipfile: "${distsDir}/unix/onCourseServer.tar.gz", src:"${distsDir}/unix/onCourseServer.tar")

        //delete temporary files from this distribution dir
        delete fileTree(dir: "${distsDir}/unix", exclude: "onCourseServer.tar.gz")
	}
}

task installer_win(dependsOn: [bundle_win, prepareKeychains], type: au.com.ish.NsisTask) {
	exeFile = file("${distsDir}/windows/onCourseServer.exe")
	resourceFolder = file("${projectDir}/src/packaging/windows")
	nsiFileName = "server-mui.nsi"
	outputFile = file("${distsDir}/windows/onCourseServer-installer.exe")

	if (project.hasProperty('osxKeychainPassword')) {
		certificateFile = file (project.codeSigningTemporaryKeystore)
		certificateFilePassword= project.codeSigningTemporaryPassword
	}
}

task installer_winService(dependsOn: [bundle_winService, prepareKeychains], type: au.com.ish.NsisTask) {
	exeFile = file("${distsDir}/windows/onCourseService.exe")
	resourceFolder = file("${projectDir}/src/packaging/windows")
	nsiFileName = "service-mui.nsi"
	outputFile = file("${distsDir}/windows/onCourseService-installer.exe")

	if (project.hasProperty('osxKeychainPassword')) {
		certificateFile = file (project.codeSigningTemporaryKeystore)
		certificateFilePassword= project.codeSigningTemporaryPassword
	}
}

task installer_winService64(dependsOn: [bundle_winService64, prepareKeychains], type: au.com.ish.NsisTask) {
	exeFile = file("${distsDir}/windows/onCourseService64.exe")
	resourceFolder = file("${projectDir}/src/packaging/windows")
	nsiFileName = "service64-mui.nsi"
	outputFile = file("${distsDir}/windows/onCourseService64-installer.exe")

	if (project.hasProperty('osxKeychainPassword')) {
		certificateFile = file (project.codeSigningTemporaryKeystore)
		certificateFilePassword= project.codeSigningTemporaryPassword
	}
}

String sha256(String filename) {
	if (! file(filename).exists()) return "missing file"

	new ByteArrayOutputStream().withStream { result ->
		def e = exec {
			executable = 'shasum' // this is only going to work on OSX
			args = ['-a', '256', filename]
			standardOutput = result
		}
		return result.toString().split()[0]
	}
}
