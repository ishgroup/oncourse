/*
 * Copyright ish group pty ltd 2020.
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the
 * GNU Affero General Public License version 3 as published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
 * without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU Affero General Public License for more details.
 */

apply from: "$rootDir/nodeSetup.gradle"

task apiDoc(type: NpmTask, dependsOn: [':server-api:apiDocs_mergeSwagger', 'npmInstall']) {
    inputs.dir('src')
    inputs.files('webpack.api.doc.config.js', 'package-lock.json', 'tsconfig.json')
    inputs.property('version', version)

    group = "documentation"
    npmCommand = ['run']
    args = ['build']

    outputs.dir("${layout.buildDirectory.get()}/api-docs")
}

task packageApiDoc (type: Zip, dependsOn: apiDoc) {
    group = "documentation"
    from apiDoc.outputs.files
    archiveClassifier = 'documentation'
}

tasks.named('build') {
    dependsOn apiDoc
}

publishing {
    publications {
        create("docs", MavenPublication) {
            artifact(packageApiDoc) {
                artifactId = "apiDoc"
            }
        }
    }
}
