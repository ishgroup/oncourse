apply plugin: 'com.github.node-gradle.node'

node {
    version = '14.16.0'
    yarnVersion = '1.22.4'
    download = System.properties['os.name'] != "FreeBSD" // FreeBSD has no binaries this plugin can install
}

yarn_install.args = ['--verbose']

task webpack(type: NodeTask, dependsOn: 'yarn_install') {
    script = file('node_modules/.bin/webpack')
    args = ['--progress', '--env', 'NODE_ENV=production', '--env', 'BUILD_NUMBER=', version ?: '99-SNAPSHOT']
}

task jest(type: NodeTask, dependsOn: 'yarn_install') {
    script = project.file('node_modules/.bin/jest')
    args = ['--ci', '--testResultsProcessor=jest-junit']
}

sourceSets {
    main.resources.srcDirs 'src/js', 'src/dev', 'build/generated-sources'
}

test.dependsOn(jest)

build.dependsOn(webpack)

publishing {
    publications {
        executables(MavenPublication) {
            artifact(file("${buildDir}/distribution/${project.name}.zip")) {
                classifier "executable"
                extension "zip"
                builtBy build
            }
        }
    }
}
configurations {
    swagger
}

dependencies {
    swagger project(':common:codegen:typescript')
}

task (swagger, type: JavaExec) {
    main = 'ish.oncourse.codegen.typescript.WillowClientGenerator'
    classpath = configurations.swagger
    args  = [ project.projectDir, "${project.rootDir}/${project.name}-api/src/main/resources/${project.name}.yaml" ]
}
