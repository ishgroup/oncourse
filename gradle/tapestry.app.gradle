/**
 * Common build file for projects which use boutique-tapestry-java8 stack
 */
apply plugin: 'java'
apply plugin: 'application'

apply from: "$rootDir/gradle/bootique.gradle"
apply from: "$rootDir/gradle/jetty.gradle"

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDir 'src/main/java'

sourceSets.test.java.srcDirs = []
sourceSets.test.groovy.srcDir 'src/test/java'

configurations {
    tapestryTest
    all*.exclude group: 'org.apache.geronimo.specs', module: 'geronimo-servlet_2.5_spec'
    all*.exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
    all*.exclude group: 'org.eclipse.jetty', module: 'jetty-plus'
}


dependencies {
    compile project(':common:common-functions')
    compile(project(':common:common-services')) {
        exclude group: 'axis', module: 'axis-wsdl4j'
    }

    compile("ish.oncourse.willow:webservices-client:$waCommonWebservicesClientVersion") {
        //we should exclude cayenne-server from waCommon because the cayenne-server is not compatable to willow code.
        exclude group: 'org.apache.cayenne', module: 'cayenne-server'
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-javamail_1.4_spec'
    }

    /**
     * bootique dependencies
     */
    compile 'io.bootique.tapestry:bootique-tapestry'

    compile 'aopalliance:aopalliance:1.0'
    compile 'commons-logging:commons-logging:1.1.1'

    /**
     * poi dependencies
     */
    compile 'org.apache.poi:poi:3.11-beta3'
    compile 'org.apache.poi:poi-ooxml:3.11-beta3'

    compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: mariadbVersion
    
    testCompile 'wsdl4j:wsdl4j:1.6.3'
    testCompile 'org.jodd:jodd-http:3.9.1'
    testCompile 'org.apache.derby:derby:10.13.1.1'
    testCompile project(':common:test-functions')

    tapestryTest("org.apache.tapestry:tapestry-test:$tapestryVersion")
}

//workaround: put org.apache.tapestry:tapestry-test to end of test classpath
sourceSets.test.compileClasspath = sourceSets.test.compileClasspath + configurations.tapestryTest
sourceSets.test.runtimeClasspath = sourceSets.test.runtimeClasspath + configurations.tapestryTest

startScripts {
    unixStartScriptGenerator.template = resources.text.fromFile("${project.rootDir}/buildSrc/src/main/resources/applicationStartScript.sh")
}

processResources.doLast {
    file("${buildDir}/resources/VERSION").text = (version && version != "99-SNAPSHOT") ? version : "99-SNAPSHOT${System.getProperty('build_number')}"
}

distZip {
    applicationDistribution.from("${buildDir}/resources/VERSION") {
        into('.')
    }
    archiveName "${baseName}.zip"
}

publishing {
// For deploying to our repo
    publications {
        executables(MavenPublication) {
            artifact(distZip) {
                classifier "executable"
                extension "zip"
            }
        }
    }
}