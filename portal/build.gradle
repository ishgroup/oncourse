apply from: "$rootDir/gradle/tapestry.app.gradle"
apply from: "$rootDir/gradle/cxf-spring.gradle"
apply from: "$rootDir/gradle/build-static.gradle"
apply plugin: 'com.moowork.node'

node {
    version = '12.9.1'
    yarnVersion = '1.22.4'
    download = true
}

yarn_install.args = ['--verbose']

task webpack(type: NodeTask, dependsOn: 'yarn_install') {
    script = file('node_modules/.bin/webpack')
    args = ['--progress', '-p', '--env.NODE_ENV', 'production', '--env.BUILD_NUMBER', version ?: '99-SNAPSHOT']
}

build.dependsOn(webpack)

mainClassName = 'ish.oncourse.portal.PortalApp'

dependencies {
    compile 'org.mnode.ical4j:ical4j:1.0.2'
    compile 'com.ocpsoft:ocpsoft-pretty-time:1.0.7'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.5'
    compile (project(':common:common-textile')) {
        exclude group: 'axis', module: 'axis-wsdl4j'
    }

    compile (project(':common:tapestry-classes'))
    // The odd next line is to work around gradle pulling in the wrong configuration
    // http://forums.gradle.org/gradle/topics/transitive_libraries_not_included_through_project_dependency
    testCompile project(':common:test-functions')
    testCompile(project(path: ':common:common-test', configuration: 'testFixtures')) {
        exclude group: 'org.apache.tapestry'
    }
}
