apply plugin: 'com.github.node-gradle.node'
apply plugin: 'queryLanguageModel'

node {
    version = '12.19.0'
    yarnVersion = '1.22.10'
    download = System.properties['os.name'] != "FreeBSD" // FreeBSD has no binaries this plugin can install
    workDir = file("${project.buildDir}/nodejs")
}

yarn.outputs.cacheIf { true }

task webpack(type: YarnTask, dependsOn: 'queryGrammar') {
    inputs.files("webpack.config.js", "tsconfig.json", "yarn.lock", "package.json", "jest.config.js")
    inputs.dir("src")
    outputs.dir("$buildDir/assets")
    inputs.property('version', version)

    args = ['build:prod', '--env', 'NODE_ENV=production', '--env', 'BUILD_NUMBER=', version ?: 'SNAPSHOT']
}

clean {
    delete "${projectDir}/node_modules"
}

task yarn_start(type: YarnTask, dependsOn:['webpack']) {
    inputs.files("webpack.config.js", "webpack.dev.config.js", "tsconfig.json", "yarn.lock", "package.json")
    args = ['start']
}

task yarn_outdated(type: YarnTask) {
    args = ['outdated']
}

task queryGrammar(type: YarnTask, dependsOn: [':server-api:swagger', 'yarn', 'generateQueryLanguageModel']) {
    inputs.dir("$rootDir/buildSrc/aql/src/main/resources/")
    outputs.dir("$buildDir/generated-sources/aql-parser")
    args = ['antlr4ts']
}

task jest(type: YarnTask, dependsOn: 'queryGrammar') {
    args = ['test', '--runInBand']
}
test.dependsOn(jest)

sourceSets {
    main.resources.srcDirs 'src/js', 'build/generated-sources/swagger-js', 'build/generated-sources/aql-model', 'build/generated-sources/aql-parser'
}

build.dependsOn(webpack)

generateQueryLanguageModel {
    source = fileTree("$rootDir/types/src/main/groovy")
            .plus(fileTree("$rootDir/types/src/main/java"))
            .plus(fileTree("$rootDir/server/src/main/java"))

    destinationDir = file("$buildDir/generated-sources/aql-model")
}

processResources.dependsOn(':server-api:swagger', "generateQueryLanguageModel")
