/*
 * Copyright ish group pty ltd 2022.
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU Affero General Public License version 3 as published by the Free Software Foundation.
 *
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more details.
 */

plugins {
    id 'queryLanguageModel'
}

apply from: "$rootDir/nodeSetup.gradle"

sourceSets {
    main.resources.srcDirs 'src/js', 'build/generated-sources/swagger-js', 'build/generated-sources/aql-model', 'build/generated-sources/aql-parser'
}

task webpack(type: NpmTask, dependsOn: 'queryGrammar') {
    inputs.files("webpack.config.js", "tsconfig.json", "package-lock.json", "package.json", "jest.config.js")
    inputs.dir("src")
    outputs.dir("${layout.buildDirectory.get()}/assets")
    inputs.property('version', version)
    npmCommand = ['run']
    args = ['build:prod', "BUILD_NUMBER=${version ?: '99-SNAPSHOT'}"]
}

task npm_start(type: NpmTask, dependsOn:['webpack']) {
    npmCommand = ['run']
    inputs.files("webpack.config.js", "webpack.dev.config.js", "tsconfig.json", "package-lock.json", "package.json")
    args = ['start']
}

task npm_outdated(type: NpmTask) {
    npmCommand = ['run']
    args = ['outdated']
}

task queryGrammar(type: NpmTask, dependsOn: [':server-api:swagger', 'npmInstall', 'generateQueryLanguageModel']) {
    inputs.dir("$rootDir/buildSrc/aql/src/main/resources/")
    outputs.dir("${layout.buildDirectory.get()}/generated-sources/aql-parser")
    npmCommand = ['run']
    args = ['antlr4ts']
}

task jest(type: NpmTask, dependsOn: 'queryGrammar') {
    npmCommand = ['run']
    args = ['test']
}

generateQueryLanguageModel {
    source = fileTree("$rootDir/types/src/main/groovy")
            .plus(fileTree("$rootDir/types/src/main/java"))
            .plus(fileTree("$rootDir/server/src/main/java"))

    destinationDir = file("${layout.buildDirectory.get()}/generated-sources/aql-model")
}

if("true" != System.getProperty('ish.devMode')) {
    tasks.named('test') {
        dependsOn jest
    }
}

tasks.named('build') {
    dependsOn webpack
}

tasks.named('processResources') {
    dependsOn(':server-api:swagger', "generateQueryLanguageModel", "queryGrammar")
}
