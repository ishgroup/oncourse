/*
 * Copyright ish group pty ltd 2022.
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU Affero General Public License version 3 as published by the Free Software Foundation.
 *
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more details.
 */

plugins {
    id 'queryLanguageModel'
}

apply from: "$rootDir/nodeSetup.gradle"


task webpack(type: NpmTask, dependsOn: 'queryGrammar') {
    inputs.files("webpack.config.js", "tsconfig.json", "package-lock.json", "package.json", "jest.config.js")
    inputs.dir("src")
    outputs.dir("$buildDir/assets")
    inputs.property('version', version)
    npmCommand = ['run']
    args = ['build:prod', '--env', 'NODE_ENV=production', '--env', "BUILD_NUMBER=${version ?: '99-SNAPSHOT'}"]
}

task npm_start(type: NpmTask, dependsOn:['webpack']) {
    npmCommand = ['run']
    inputs.files("webpack.config.js", "webpack.dev.config.js", "tsconfig.json", "package-lock.json", "package.json")
    args = ['start']
}

task npm_outdated(type: NpmTask) {
    npmCommand = ['run']
    args = ['outdated']
}

task queryGrammar(type: NpmTask, dependsOn: [':server-api:swagger', 'npmInstall', 'generateQueryLanguageModel']) {
    inputs.dir("$rootDir/buildSrc/aql/src/main/resources/")
    outputs.dir("$buildDir/generated-sources/aql-parser")
    npmCommand = ['run']
    args = ['antlr4ts']
}

task jest(type: NpmTask, dependsOn: 'queryGrammar') {
    npmCommand = ['run']
    args = ['test']
}
test.dependsOn(jest)

sourceSets {
    main.resources.srcDirs 'src/js', 'build/generated-sources/swagger-js', 'build/generated-sources/aql-model', 'build/generated-sources/aql-parser'
}

build.dependsOn(webpack)

generateQueryLanguageModel {
    source = fileTree("$rootDir/types/src/main/groovy")
            .plus(fileTree("$rootDir/types/src/main/java"))
            .plus(fileTree("$rootDir/server/src/main/java"))

    destinationDir = file("$buildDir/generated-sources/aql-model")
}

processResources.dependsOn(':server-api:swagger', "generateQueryLanguageModel")
