ext {
    servicesPackage = 'ish.oncourse.willow.editor.v1.service'
    modelPackage = 'ish.oncourse.willow.editor.v1.model'
    swaggerName = 'editor.yaml'
}

apply from: "$rootDir/gradle/functions.gradle"
apply from: "$rootDir/gradle/build-api.gradle"
apply from: "$rootDir/gradle/codegen.gradle"


mainClassName = 'ish.oncourse.willow.editor.EditorApp'
targetCompatibility = '11'
sourceCompatibility = '11'

configurations {
    closureCompiler
}

dependencies {
    implementation project(':common:common-api')
    implementation project(':common:common-templates')
    implementation project(':common:common-bootique')

    //webdav servlet
    implementation 'io.milton:milton-api:2.6.0.1'
    implementation 'io.milton:milton-server-ce:2.6.0.1'

    implementation 'org.apache.commons:commons-compress:1.9'
    implementation 'org.reflections:reflections:0.9.11'

    //js compiler
    implementation 'com.google.javascript:closure-compiler:v20170218'
    closureCompiler 'com.google.javascript:closure-compiler:v20170218'

    // JAX-B dependencies for JDK 11
    implementation 'javax.xml.ws:jaxws-api:2.3.1'

    //argon2 crypto algorithm
    implementation "org.bouncycastle:bcprov-jdk15on:1.67"

}

test {
    forkEvery = 1 //run every test in separate jvm
    initTestDBConfig(project, test)
}

publishing {
// For deploying to our repo
    publications {
        docs(MavenPublication) {
            artifact("${buildDir}/dist/doc.zip") {
                artifactId "editor-api-docs"
                classifier "documentation"
                extension "zip"
                builtBy packageDoc
            }
        }
    }
}
